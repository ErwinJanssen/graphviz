BISON_TARGET(Exparse exparse.y ${CMAKE_CURRENT_BINARY_DIR}/exparse.c)

# Modify files generated by Bison to match the Autotools build.
# - Replace "yy" with "ex"
# - Replace "YY" with "EX"
# - Replace "unsigned long int" with "uint64_t"
# - Replace "unsigned long" with "uint64_t"
# - Replace "fprintf" with "sfprintf"
# - Replace "FILE" with "Sfio_t"
# - Replace "stderr" with "sfstderr"
configure_file(
    "${TOP_SOURCE_DIR}/cmake/modify_expr_exparse.cmake.in"
    "${CMAKE_CURRENT_BINARY_DIR}/modify_expr_exparse.cmake"
    @ONLY
)
add_custom_command(
    OUTPUT ${BISON_Exparse_OUTPUTS}
    COMMAND ${CMAKE_COMMAND} -P ${CMAKE_CURRENT_BINARY_DIR}/modify_expr_exparse.cmake
    APPEND
)

# Create exop.h based n exparse.h
configure_file(
    "${TOP_SOURCE_DIR}/cmake/create_expr_exop.cmake.in"
    "${CMAKE_CURRENT_BINARY_DIR}/create_expr_exop.cmake"
    @ONLY
)
add_custom_command(
    OUTPUT exop.h
    COMMAND ${CMAKE_COMMAND} -P ${CMAKE_CURRENT_BINARY_DIR}/create_expr_exop.cmake
    DEPENDS ${BISON_Exparse_OUTPUTS}
)

include_directories(
    ${CMAKE_CURRENT_SOURCE_DIR}
    ${CMAKE_CURRENT_BINARY_DIR}
    ${GRAPHVIZ_LIB_DIR}/ast
    ${GRAPHVIZ_LIB_DIR}/cdt
    ${GRAPHVIZ_LIB_DIR}/sfio
    ${GRAPHVIZ_LIB_DIR}/vmalloc
)

add_library(expr STATIC
    # Header files
    exgram.h
    exlib.h
    expr.h

    # Source files
    excc.c
    excontext.c
    exdata.c
    exerror.c
    exeval.c
    exexpr.c
    exlexname.c
    exnospace.c
    exopen.c
    exrewind.c
    exstash.c
    extoken.c
    extype.c
    exzero.c

    # Generated files
    ${BISON_Exparse_OUTPUTS}
    exop.h
)

# Specify man pages to be installed
install(
    FILES expr.3
    DESTINATION ${MAN_INSTALL_DIR}
)
